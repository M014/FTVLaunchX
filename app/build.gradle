// App-specific build settings.

apply plugin: 'com.android.application'

def versionMajor = 0
def versionMinor = 0
def versionPatch = 1
def versionSuffix = "-dev"

def keystorePropertiesFile = rootProject.file('keystore.properties')
def keystoreProperties = new Properties()

if (keystorePropertiesFile.exists()) {
	keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
} else {
	keystoreProperties['storeFile'] = 'none'
}

android {
	compileSdkVersion 29
	buildToolsVersion "29.0.2"

	defaultConfig {
		applicationId "de.codefaktor.ftvlaunchx"

		minSdkVersion 22 // Android 5.1
		targetSdkVersion 28 // Android 9.0

		versionCode versionMajor * 1000000 + \
					versionMinor * 1000 + \
					versionPatch
		versionName "${versionMajor}." + \
					"${versionMinor}." + \
					"${versionPatch}"
		versionNameSuffix "${versionSuffix}"

		resValue "string", "app_version", \
				 "${versionName}${versionNameSuffix}"
	}

	signingConfigs {
		release {
			storeFile file(keystoreProperties['storeFile'])
			storePassword keystoreProperties['storePassword']
			keyAlias = keystoreProperties['keyAlias']
			keyPassword keystoreProperties['keyPassword']
		}
	}

	buildTypes {
		release {
			minifyEnabled false
			proguardFiles \
				getDefaultProguardFile('proguard-android-optimize.txt'), \
				'proguard-rules.pro'
			signingConfig signingConfigs.release
		}
	}
}

dependencies {
	implementation fileTree(dir: 'libs', include: ['*.jar'])
	implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
	implementation "androidx.leanback:leanback:1.0.0"
}
